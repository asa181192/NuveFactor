Imports FactorEntidades
Imports System.Data.SqlClient

Public Class CesionesDAL

#Region "Constructor"

#End Region

#Region "Metodos de consulta"

	Public Function ConsultaCesionesDAL(Contrato As Integer, ByRef model As List(Of CesionEntidad)) As Result

		Dim respuesta = New Result(False)

		Using context As New FactorContext
			Try

				Dim parameters = New List(Of SqlParameter)
				parameters.Add(New SqlParameter("@Contrato", Contrato))
				model = context.Database.SqlQuery(Of CesionEntidad)("Exec [dbo].[SP_ConsultaCesiones] " +
																		 " @Contrato ", parameters.ToArray()).ToList()

				respuesta.Ok = True
			Catch e As Exception
				respuesta.Detalle = e.Message
			End Try

		End Using

		Return respuesta

	End Function

	Public Function ConsultaDoctosDAL(contrato As Integer, cesion As Integer, ByRef model As List(Of DoctosEntidad)) As Result

		Dim respuesta = New Result(False)

		Using context As New FactorContext
			Try


				model = (From A In context.doctos
						From B In context.comprador.Where(Function(w) w.deudor = A.deudor).DefaultIfEmpty()
						Where A.contrato = contrato And A.cesion = cesion
						Select New DoctosEntidad With {
										.iddocto = A.iddocto,
										.docto = A.docto,
										.fec_vence = A.fec_vence,
										.importe = A.importe,
										.descto = A.descto,
										.monto = A.monto,
										.interes = A.interes,
										.hono = A.hono,
										.nombreDeudor = B.nombre
										}).ToList()

				respuesta.Ok = True
			Catch e As Exception
				respuesta.Detalle = e.Message
			End Try

		End Using

		Return respuesta

	End Function

	Public Function ConsultaGarantiaDAL(contrato As Integer, cesion As Integer, ByRef model As List(Of GarantiaEntidad)) As Result

		Dim respuesta = New Result(False)

		Using context As New FactorContext
			Try
				model = (From A In context.garantia
						From B In context.tipogarantia.Where(Function(w) w.tipoid = A.tipo).DefaultIfEmpty()
						Where A.contrato = contrato And A.cesion = cesion
						Select New GarantiaEntidad With {
											.garantiaid = A.garantiaid,
											.nombreTipo = B.nombre,
											.cobrado = A.cobrado,
											.costo = A.costo,
											.porcentaje = A.porcentaje,
											.valor = A.valor
										}).ToList()

				respuesta.Ok = True
			Catch e As Exception
				respuesta.Detalle = e.Message
			End Try

		End Using

		Return respuesta

	End Function

	Function ConsultaDetalleCesionDAL(cesion As Integer, ByRef model As CesionEntidad, contrato As Integer) As Result

		Dim respuesta = New Result(False)
		Using context As New FactorContext

			Try


				model = (From A In context.cesiones
						 Join B In context.ctaprove
						 On A.numreccta Equals B.numrec
						 From C In context.proveedor.Where(Function(x) x.deudor = A.proveedor).DefaultIfEmpty()
						 Where A.cesion = cesion And A.contrato = contrato
										Select New CesionEntidad With {
															.cesion = A.cesion,
															.fec_alta = A.fec_alta,
															.doctos = A.doctos,
															.tasaoper = A.tasaoper,
															.tasahono = A.tasahono,
															.importe = A.importe,
															.interes = A.interes,
															.honorario = A.honorario,
															.ivahonorario = A.ivahonorario,
															.garantnafin = A.garantnafin,
															.ivaganafin = A.ivaganafin,
															.totalpagar = A.totalpagar,
															.impanticipado = A.impanticipado,
															.proveedor = A.proveedor,
															.numreccta = A.numreccta,
															.clabe = B.clabe,
															.proveedorNombre = C.nombre
																}).SingleOrDefault()
				If model IsNot Nothing Then
					respuesta.Ok = True
				End If

			Catch e As Exception
				respuesta.Detalle = e.Message
				respuesta.Id_Out = 1
			End Try

		End Using
		Return respuesta
	End Function

	Function ConsultaDetalleGarantiaDAL(garantiaid As Integer, ByRef model As GarantiaEntidad) As Result

		Dim respuesta = New Result(False)
		Using context As New FactorContext

			Try
				model = context.garantia.Where(Function(x) x.garantiaid = garantiaid).Select(Function(x) New GarantiaEntidad With {
																												   .codigo = x.codigo,
																												   .tipo = x.tipo,
																												   .valor = x.valor,
																												   .porcentaje = x.porcentaje,
																												   .costo = x.costo,
																												   .cobrado = x.cobrado,
																												   .ivacobrado = x.ivacobrado
																												   }).SingleOrDefault()

				If model IsNot Nothing Then
					respuesta.Ok = True
				End If

			Catch e As Exception
				respuesta.Detalle = e.Message
				respuesta.Id_Out = 1
			End Try

		End Using
		Return respuesta
	End Function

#End Region

#Region "Metodos Transaccionales"
	Public Function ActualizarGarantiaDAL(model As garantia) As Result
		Dim respuesta = New Result(False)

		Using context As New FactorContext

			Try
				Dim garantia = context.garantia.Where(Function(x) x.garantiaid = model.garantiaid).SingleOrDefault()

				If garantia IsNot Nothing Then
					garantia.codigo = model.codigo
					garantia.tipo = model.tipo
					garantia.valor = model.valor
					garantia.porcentaje = model.porcentaje
					garantia.costo = model.costo
					garantia.cobrado = model.cobrado
					garantia.ivacobrado = model.ivacobrado
				End If
			
				context.SaveChanges()
				respuesta.Ok = True
			Catch e As Exception
				respuesta.Detalle = e.InnerException.InnerException.Message
			End Try

		End Using

		Return respuesta
	End Function
#End Region


End Class
